load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_ecsact//ecsact:toolchain.bzl", "ecsact_toolchain")
load("@rules_ecsact//ecsact:defs.bzl", "ecsact_codegen")

platform(
    name = "windows-x86_64",
    constraint_values = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
)

copts = select({
    "@bazel_tools//tools/cpp:msvc": ["/std:c++latest", "/Zc:preprocessor", "/permissive-"],
    "//conditions:default": ["-std=c++20"],
})

ecsact_toolchain(
    name = "ecsact_cli_windows",
    target_tool = "@ecsact_cli_windows//file",
    # target_tool_path = "C:/Users/zekew/projects/ecsact-dev/ecsact_cli/bazel-bin/ecsact.exe",
)

ecsact_toolchain(
    name = "ecsact_cli_linux",
    target_tool = "@ecsact_cli_linux//file",
)

toolchain(
    name = "ecsact_windows_toolchain",
    exec_compatible_with = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
    toolchain = ":ecsact_cli_windows",
    toolchain_type = "@rules_ecsact//ecsact:toolchain_type",
)

toolchain(
    name = "ecsact_linux_toolchain",
    exec_compatible_with = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
    toolchain = ":ecsact_cli_linux",
    toolchain_type = "@rules_ecsact//ecsact:toolchain_type",
)

ecsact_srcs = [
    "example.ecsact",
    "example_a.ecsact",
    "example_b.ecsact",
]

ecsact_codegen(
    name = "ecsact_cc_system_impl_srcs",
    srcs = ecsact_srcs,
    plugins = [
        "@ecsact_lang_cpp//cpp_systems_source_codegen",
    ],
)

ecsact_codegen(
    name = "ecsact_cc_hdrs",
    srcs = ecsact_srcs,
    plugins = [
        "@ecsact_lang_cpp//cpp_header_codegen",
        "@ecsact_lang_cpp//cpp_systems_header_codegen",
        "@ecsact_lang_cpp//systems_header_codegen",
        "@ecsact_lang_cpp//cpp_meta_header_codegen",
    ],
)

cc_library(
    name = "ecsact_cc",
    hdrs = [":ecsact_cc_hdrs"],
    copts = copts,
    strip_include_prefix = "ecsact_cc_hdrs",
    deps = [
        "@ecsact_lang_cpp//:execution_context",
    ],
)

cc_library(
    name = "meta_check",
    copts = copts,
    srcs = ["meta_check.cc"],
    deps = [
        "//:ecsact_cc",
        "@ecsact_lang_cpp//:type_info",
        "@ecsact_runtime//:lib",
    ],
)

cc_binary(
    name = "example_system_impls",
    copts = copts,
    srcs = [
        "system_impls.cc",
        "//:ecsact_cc_system_impl_srcs",
    ],
    linkshared = True,
    deps = [
        "//:ecsact_cc",
        "@ecsact_runtime//dylib:dynamic",
    ],
)
